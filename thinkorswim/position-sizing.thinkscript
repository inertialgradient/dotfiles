# TODO: user-defined percentage of account size per trade
# (risky: 2.5% of TAS, day/swing: 10%, half position: 5%)

input displayLabel = yes;

# percentage of account to be kept in reserve
input cashReservePercentage = 33;

# maximum percentage of unreserved cash to risk on any given trade
input maxRiskPercentage = 10;

# is the current security an option?
def isOption = if !isNan(GetDaysToExpiration()) then yes else no;

# use the current account's cash balance as the overall balance.
# for safety, ignore margin and reduce for options
# note: GetTotalCash() only works if Show Extended-Hours Trading is enabled
def availableCash = if isOption then GetTotalCash() * 0.5 else GetTotalCash();

# scaling factor to get how much available cash is tradeable
def unreservedCash = (1 - (cashReservePercentage / 100));

# scaling factor to get the maximum to be risked on any given trade
def maxRisk = maxRiskPercentage / 100;

# how much cash can be deployed on a given trade
def tradeableCash = availableCash * unReservedCash * maxRisk;

# the cost of a single share or contract of the given security
def unitCost = if isOption then close * 100 else close;

def maxUnitsBuyable = RoundDown(tradeableCash / unitCost, 0);
def currPositionSize = GetQuantity();

def shouldDisplay =
  if !isNaN(maxUnitsBuyable)
  and !isNaN(currPositionSize)
  and maxUnitsBuyable > 0
  then displayLabel
  else no;

AddLabel(
  shouldDisplay,
  "posn: " + AsText(currPositionSize) + "/" + AsText(maxUnitsBuyable),
  Color.GRAY
);
